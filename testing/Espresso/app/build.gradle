apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.lft.espressointro"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        //needed for espresso
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

/**
 * Here espresso dependency is support annotation 23.0.1
 * but by default the appcompat is using support annotation 23.2.0
 * due to which conflict arises, so when we do the following
 * all this project dependency must depend on 23.2.0 version
 * https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.ResolutionStrategy.html
 * https://sites.google.com/a/android.com/tools/tech-docs/new-build-system/user-guide#TOC-Testing*/
configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:23.2.0'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'io.realm:realm-android:0.87.5'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.1'

    compile 'com.github.thorbenprimke:realm-recyclerview:0.9.12'
    compile 'com.android.support:recyclerview-v7:23.2.0'

    compile 'com.loopj.android:android-async-http:1.4.9'


    //for espresso setup
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    //for espresso contrib for datepicker,drawerlayout and recyclerview
    //http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    //for intent tests with espresso
    // Espresso Intents
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.1'

    //for mockito setup
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    //these are needed for to run test on android i.e. to mock Android classes
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    testCompile 'org.mockito:mockito-core:1.10.19'
}
